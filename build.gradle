plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "checkstyle"
    id "net.raphimc.class-token-replacer" version "1.1.2"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

allprojects {
    apply plugin: "checkstyle"

    archivesBaseName = project.maven_name
    group = project.maven_group
    version = project.maven_version

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = "9.3"
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    }
    build.dependsOn(check)
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.version)
        }
    }
}

dependencies {
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    api "io.github.spair:imgui-java-binding:1.86.11"
    api "com.google.guava:guava:33.2.1-jre"
    api "net.lenni0451.mcstructs:nbt:2.4.2"
    api "net.lenni0451.mcstructs:snbt:2.4.2"
    api "net.lenni0451.mcstructs-bedrock:nbt:1.2.0"
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption("Xdoclint:none", "-quiet")
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            def releasesUrl = "https://maven.lenni0451.net/releases"
            def snapshotsUrl = "https://maven.lenni0451.net/snapshots"
            url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.maven_name
            groupId = project.maven_group
            version = project.maven_version

            from components.java

            pom {
                name = rootProject.name
                description = "A template for gradle projects"
                url = "https://github.com/Lenni0451/ImNbt"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/Lenni0451/ImNbt/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "Lenni0451"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Lenni0451/ImNbt.git"
                    developerConnection = "scm:git:ssh://github.com/Lenni0451/ImNbt.git"
                    url = "github.com/Lenni0451/ImNbt"
                }
            }
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
